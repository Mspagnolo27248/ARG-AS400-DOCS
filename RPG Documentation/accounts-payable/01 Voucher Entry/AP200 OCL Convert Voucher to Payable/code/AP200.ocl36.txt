**
** PURCHASE JOURNAL (POST VOUCHER TRANSACTIONS)
**
** JB01  091414  JAN BECCARI
**       REVISE TO POST RECEIPT TO INVENT TRANS HOLDING (INTZH), TOO
**
** JK01  032715  JIMMY KRAJACIC
**       REVISE TO ADD FILE FRCINV, TO UNCODE CARRIER FREIGHT INVOICES
**       WHEN A VOUCHER HAS BEEN DELETED IN A BATCH
// SCPROCP ,,,,,,,,?9?
**
// SWITCH 00000000
**
** "WT" MEANS USER RAN "WIRE TRANSFER" JOURNAL MENU OPTION
// IF ?3?/WT      EVALUATE P20='APWT?WS?'
// ELSE           EVALUATE P20='APTR?WS?'
**
// IFF DATAF1-?9??20?                 SWITCH 10000000
// IF  ?F'A,?9??20?'?/00000000        SWITCH 10000000
**
// IF  SWITCH1-1        * 'NO A/P VOUCHER OR WIRE TRANSACTION FILE TO POST'
// IF  SWITCH1-1        PAUSE 'PRESS 0,ENTER AND JOB WILL BE CANCEL'
// IF  SWITCH1-1        GOTO END
**
// GSY2K
**
// IF  DATAF1-?9?APPT?WS?   IF ?F'A,?9?APPT?WS?'?/00000000 +
                             DELETE ?9?APPT?WS?,F1
// IF  DATAF1-?9?APPT?WS?   * '* * * * * * * * * * * * * * * * * * * *'
// IF  DATAF1-?9?APPT?WS?   * '* THE PAYMENT CYCLE MUST BE ENDED     *'
// IF  DATAF1-?9?APPT?WS?   * '* FROM THIS WORKSTATION BEFORE THIS   *'
// IF  DATAF1-?9?APPT?WS?   * '* PURCHASE JOURNAL CAN BE RUN         *'
// IF  DATAF1-?9?APPT?WS?   * '* * * * * * * * * * * * * * * * * * * *'
// IF  DATAF1-?9?APPT?WS?   PAUSE 'PRESS 0,ENTER AND JOB WILL CANCEL'
// IF  DATAF1-?9?APPT?WS?   GOTO END
**
// IF ACTIVE-AP200           * 'AN A/P PURCHASE REGISTER IS ALREADY IN PROGRESS'
// IF ACTIVE-AP200           * 'PLEASE TRY AGAIN IN A FEW MINUTES'
// IF ACTIVE-AP200           PAUSE 'PRESS 0,ENTER AND JOB WILL CANCEL'
// IF ACTIVE-AP200           GOTO END
**
**
** JB 06/07/2021 TO STOP THE SITUATION OF AP VOUCHER POSTING TO HAPPEN
** BETWEEN BEGINNING OF WEEK AND INTS__ TRANSACTION POSTING
// IF DATAF1-?9?INTSZZ       * 'INVENTORY BEGINNING OF WEEK IS IN PROGRESS '
// IF DATAF1-?9?INTSZZ       * 'PLEASE TRY AGAIN IN A FEW MINUTES'
// IF DATAF1-?9?INTSZZ       PAUSE 'PRESS 0,ENTER AND JOB WILL CANCEL'
// IF DATAF1-?9?INTSZZ       GOTO END
**
**
*******CHECK ?9?APSTAT FOR RECORD CREATED FROM AP110 TO SEE IF THERE WERE ERRORS
*******IN THE VOUCHER TABLE.......IF YES DO NOT ALLOW POSTING
**
// AP200A ,,,,,,,,?9?,,,,,,,,,,,?20?
**
// IF ?L'231,3'?/YES * 'ERRORS EXIST IN THE BATCH.   PLEASE RETURN TO THE BATCH'
// IF ?L'231,3'?/YES  * 'AND CORRECT THE ERRORS.'
// IF ?L'231,3'?/YES  PAUSE
// IF ?L'231,3'?/YES  GOTO END
**
**
// LOCAL BLANK-*ALL
// GSY2K
**
// * 'PURCHASE JOURNAL'
**
// IF ?3?/WT  LOCAL OFFSET-198,DATA-'WT*** WIRE TRANSFER ***'
// ELSE       LOCAL OFFSET-198,DATA-'                       '
**
// LOCAL OFFSET-300,DATA-'?WS?'
**
// BLDFILE ?9?APPT?WS?,I,RECORDS,200,128,,,2,5
**
// LOAD AP200P
// FILE NAME-APTRAN,LABEL-?9??20?
// FILE NAME-APPYTR,LABEL-?9?APPT?WS?
// FILE NAME-GSTABL,LABEL-?9?GSTABL,DISP-SHR
// FILE NAME-GSCONT,LABEL-?9?GSCONT,DISP-SHR
// FILE NAME-GLCONT,LABEL-?9?GLCONT,DISP-SHR
// RUN
**
// IF ?L'129,6'?/CANCEL      GSDELETE APPT?WS?,,,,,,,,?9?
// IF ?L'129,6'?/CANCEL      GOTO END
**
// ATTR INQUIRY-YES,CANCEL-NO
**
// GSDELETE APPJ?WS?,APPK?WS?,APJC?WS?,,,,,,?9?
// BLDFILE ?9?APJC?WS?,I,RECORDS,999000,128,,,2,5
**
// * 'PURCHASE REGISTER EXECUTING'
**
// LOAD #GSORT
// FILE NAME-INPUT,LABEL-?9??20?
// FILE NAME-OUTPUT,LABEL-?9?APXX?WS?,RECORDS-999000,EXTEND-999000
// RUN
     HSORTR    15A        3X 404   N
     FNC   2   3                       COMPANY
     FNC  12  16                       VENDOR
     FNC   4  11                       ENTRY / ENTRY SEQ
     FDC   1 256
     FDC 257 404
// END
**
// LOAD AP200
// FILE NAME-APTRAN,LABEL-?9?APXX?WS?
// FILE NAME-APCONT,LABEL-?9?APCONT,DISP-SHR
// FILE NAME-APVEND,LABEL-?9?APVEND,DISP-SHR
// FILE NAME-APOPEN,LABEL-?9?APOPEN,DISP-SHR
// FILE NAME-APOPENH,LABEL-?9?APOPNH,DISP-SHR
// FILE NAME-APOPEND,LABEL-?9?APOPND,DISP-SHR
// FILE NAME-APOPENV,LABEL-?9?APOPNV,DISP-SHR
// FILE NAME-APHISTH,LABEL-?9?APHSTH,DISP-SHR
// FILE NAME-APHISTD,LABEL-?9?APHSTD,DISP-SHR
// FILE NAME-APHISTV,LABEL-?9?APHSTV,DISP-SHR
// FILE NAME-APINVH,LABEL-?9?APINVH,DISP-SHR
// FILE NAME-POFILEH,LABEL-?9?POFILH,DISP-SHR
// FILE NAME-POFILED,LABEL-?9?POFILD,DISP-SHR
// FILE NAME-JCTRAN,LABEL-?9?APJC?WS?,EXTEND-100
// FILE NAME-APPJJR,LABEL-?9?APPJ?WS?,RECORDS-999000,EXTEND-100
// FILE NAME-APPYTR,LABEL-?9?APPT?WS?,EXTEND-100
// FILE NAME-FRCINH,LABEL-?9?FRCINH,DISP-SHR
// FILE NAME-FRCFBH,LABEL-?9?FRCFBH,DISP-SHR
// IF ?9?/G  OVRPRTF FILE(APPRINT) OUTQ(QUSRSYS/APPOST)
// IFF ?9?/G OVRPRTF FILE(APPRINT) OUTQ(QUSRSYS/TESTOUTQ)
// RUN
**
// * 'PURCHASE JOURNAL EXECUTING'
**
// LOAD #GSORT
// FILE NAME-INPUT,LABEL-?9?APPJ?WS?
// FILE NAME-OUTPUT,LABEL-?9?APPK?WS?,RECORDS-999000,EXTEND-999000
// RUN
     HSORTA    21A        3X       N
     I C   1   1NECD
     I*
     FNC   2   3                       COMPANY
     FNC  12  12                       C,D
     FNC 106 115                       AP,EXPENSE,INTER-CO
     FNC  13  20                       G/L #
// END
**
**
// LOAD AP205
// FILE NAME-APPJJR,LABEL-?9?APPJ?WS?
// FILE NAME-AP205S,LABEL-?9?APPK?WS?
// FILE NAME-APCONT,LABEL-?9?APCONT,DISP-SHR
// FILE NAME-TEMGEN,LABEL-?9?TEMGEN,DISP-SHR
// IF ?9?/G  OVRPRTF FILE(APPRINT) OUTQ(QUSRSYS/APPOST)
// IFF ?9?/G OVRPRTF FILE(APPRINT) OUTQ(QUSRSYS/TESTOUTQ)
// RUN
**
** JC200 ONLY IF JOB COST TRAN RECORDS EXIST
**    AND IF JC200 PROCEDURE EXISTS
**
// IFF ?F'A,?9?APJC?WS?'?/00000000  IF PROC-'JC200,?CLIB?' +
                                     JC200 APJC?WS?,,,,,,,,?9?
** ************************************************************************
**
** POST AP INVOICES TO INVENTORY RECEIPTS
**
// * 'A/P PURCHASE RECEIPTS POST'
// LOAD AP210
// FILE NAME-APTRAN,LABEL-?9??20?
// FILE NAME-INFIL1,LABEL-?9?INFIL1,DISP-SHR
// FILE NAME-INTZH1,LABEL-?9?INTZH1,DISP-SHR
// PRINTER NAME-APLIST,PRIORITY-0,DEVICE-PJ,FORMSNO-JBAP
// RUN
**
** ************************************************************************
// GSDELETE APPJ?WS?,APPK?WS?,APTX?WS?,APXX?WS?,,,,,?9?
// GSDELETE ?20?,APCT?WS?,APJC?WS?,,,,,,?9?
**
// IF ?F'A,?9?APPT?WS?'?/00000000     GSDELETE APPT?WS?,,,,,,,,?9?
// IFF DATAF1-?9?APPT?WS?             GOTO END
**
// * '* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * '
// * '* *     AUTOMATIC PROCESSING OF PREPAID INVOICES IS EXECUTING     * * '
// * '* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * '
**
// AP150 ,AUTO,?3?,,,,,,?9?
// AP160 ,AUTO,?3?,,,,,,?9?
// AP250 ,AUTO,?3?,,,,,,?9?
**
// TAG END
**
** IF THIS WAS A BATCH CREATED THRU THE LMS SYSTEM, DELETE THE IDENTIFIER FILE
// IF DATAF1-?9?LMS?WS?   GSDELETE LMS?WS?,,,,,,,,?9?
**
// LOCAL BLANK-*ALL
// SWITCH 00000000
**
